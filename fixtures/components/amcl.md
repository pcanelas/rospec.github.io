---
title: AMCL Node Example
sidebar: home_sidebar
permalink: amcl-node
parent: Components
summary: Learn how to write rospec specifications for ROS components.
---

## AMCL Node Example

```rospec
type alias LaserModelType: Enum[Beam, LikelihoodField, LikelihoodFieldProb]

node type amcl_type {
    context distribution: AfterHumbleVersion;

    param robot_model_type: Enum[DifferentialMotionModel, OmniMotionModel];
    param scan_topic_name: string;
    param map_topic_name: string;

    optional param z_hit: double = 0.5;
    optional param z_max: double = 0.05;
    optional param z_rand: double = 0.5;
    optional param z_short: double = 0.005;
    optional param always_reset_initial_pose: bool = false;
    optional param laser_model_type: LaserModelType = LikelihoodField;

    @qos{sensor_data}
    publishes to particle_cloud: nav2_msgs/ParticleCloud;

    @qos{sensor_data_profile}
    subscribes to content(scan_topic_name): RestrictedLaserScan;

    @qos{system_default_qos}
    subscribes to initialpose: geometry_msgs/PoseWithCovarianceStamped where {count(publishers(_)) == 1};

    @qos{transient_reliable_qos}
    subscribes to content(map_topic_name): nav_msgs/OccupancyGrid;

    provides service reinitialize_global_localization: std_srvs/Empty;
    provides service set_initial_pose: nav2_msgs/SetInitialPose;

    broadcast map to odom;
    broadcast odom to base_link;
    broadcast base_link to scan;
  
} where {
    laser_model_type == Beam -> z_hit + z_max + z_rand + z_short == 1;
    laser_model_type == LikelihoodField -> z_hit + z_rand == 1;
    always_reset_initial_pose -> exists(initial_pose);
}

system {
    node instance amcl: amcl_type {
        param robot_model_type = DifferentialMotionModel;
        param scan_topic_name = "scan";
        param map_topic_name = "map";
        param z_hit = 0.7;
        param z_rand = 0.3;
        param laser_model_type = LikelihoodField;
    }
}
```